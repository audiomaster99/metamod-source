# Metamod:Source
# Copyright (C) 2003-2024 AlliedModders LLC and authors
# Licensed under the GPLv3 license. See LICENSE file in the project root for details.

if(NOT METAMOD_DIR)
	message(FATAL_ERROR "METAMOD_DIR is empty")
endif()

if(NOT SOURCESDK_DIR)
	message(FATAL_ERROR "SOURCESDK_DIR is empty")
endif()

set(METAMOD_LOADER_SOURCE_FILES
	${METAMOD_LOADER_SOURCE_FILES}

	${METAMOD_LOADER_DIR}/gamedll.cpp
	${METAMOD_LOADER_DIR}/loader.cpp
	${METAMOD_LOADER_DIR}/serverplugin.cpp
	${METAMOD_LOADER_DIR}/utility.cpp
)

set(METAMOD_LOADER_COMPILE_DEFINITIONS
	${METAMOD_LOADER_COMPILE_DEFINITIONS}

	LIB_PREFIX="${CMAKE_SHARED_LIBRARY_PREFIX}"
	LIB_SUFFIX="${CMAKE_SHARED_LIBRARY_SUFFIX}"
)

set(METAMOD_LOADER_INCLUDE_DIRS
	${METAMOD_CORE_SOURCEHOOK_DIR}
)

if(LINUX)
	set(PLATFORM_LINK_OPTIONS
		${PLATFORM_LINK_OPTIONS}

		-Wl,--version-script,${METAMOD_SYMBOLS_DIR}/loader.lds
	)
endif()

set(LOADER_PROJECT_OUTPUT_NAME "server")

# The loader library.
add_library(${LOADER_PROJECT_NAME} SHARED ${METAMOD_LOADER_SOURCE_FILES})

set_target_properties(${LOADER_PROJECT_NAME} PROPERTIES
	C_STANDARD 17
	C_STANDARD_REQUIRED ON
	C_EXTENSIONS OFF

	CXX_STANDARD 17
	CXX_STANDARD_REQUIRED ON
	CXX_EXTENSIONS OFF
)

set_target_properties(${LOADER_PROJECT_NAME} PROPERTIES OUTPUT_NAME ${LOADER_PROJECT_OUTPUT_NAME})

if(WINDOWS)
	set_target_properties(${LOADER_PROJECT_NAME} PROPERTIES MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
elseif(MACOS)
	set_target_properties(${LOADER_PROJECT_NAME} PROPERTIES OSX_ARCHITECTURES "x86_64")
endif()

target_compile_options(${LOADER_PROJECT_NAME} PRIVATE ${COMPILER_OPTIONS})
target_link_options(${LOADER_PROJECT_NAME} PRIVATE ${LINK_OPTIONS} ${PLATFORM_LINK_OPTIONS})

target_compile_definitions(${LOADER_PROJECT_NAME} PRIVATE ${COMPILE_DEFINITIONS} ${METAMOD_LOADER_COMPILE_DEFINITIONS})
target_include_directories(${LOADER_PROJECT_NAME} PRIVATE ${METAMOD_LOADER_INCLUDE_DIRS})

target_link_libraries(${LOADER_PROJECT_NAME} PRIVATE ${LINK_LIBRARIES} ${SOURCESDK_BINARY_DIR})
